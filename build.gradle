plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'java'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

version = project.mod_version
group = project.maven_group

repositories {
    maven { name = "Terraformers"; url = "https://maven.terraformersmc.com/" }
    maven { name = "meteor-maven"; url = "https://maven.meteordev.org/releases" }
}

configurations {
    libImpl
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    libImpl "com.fasterxml.jackson.core:jackson-databind:${project.jackson_version}"
    libImpl "meteordevelopment:orbit:0.2.3"

    configurations.libImpl.dependencies.each { implementation(it) }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.libImpl.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name')
        )
    }
}

processResources {
    filesMatching('fabric.mod.json') {
        expand(
                version: project.mod_version,
                minecraft_version: project.minecraft_version,
                loader_version: project.loader_version
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}